apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  compileSdkVersion 31
  defaultConfig {
    applicationId "net.emojiparty.android.jishotomo"
    minSdkVersion 21
    targetSdkVersion 31
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  signingConfigs {
    config {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.config
    }
    debug {
      applicationIdSuffix ".debug"
      debuggable true
    }
  }
  buildFeatures {
    dataBinding true
    viewBinding true
    compose true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
  kotlinOptions {
    jvmTarget = "11"
  }
  composeOptions {
    kotlinCompilerExtensionVersion '1.1.1'
  }
  testOptions {
    animationsDisabled = true
  }
}

configurations {
  ktlint
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')

  ktlint "com.pinterest:ktlint:0.37.2"

  implementation 'androidx.appcompat:appcompat:1.4.1'
  implementation 'com.google.android.material:material:1.5.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

  def lifecycle_version = "2.4.1"
  def room_version = "2.4.2"
  def android_test_version = "1.4.0"
  def espresso_version = "3.4.0"

  // ViewModel and LiveData
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

  // Room
  implementation "androidx.room:room-runtime:$room_version"
  kapt "androidx.room:room-compiler:$room_version"
  // Paging
  implementation "androidx.paging:paging-runtime:2.1.2"

  // Dagger 2
  implementation "com.google.dagger:dagger-android-support:2.13"
  implementation "com.google.dagger:dagger:2.13"
  kapt 'com.google.dagger:dagger-compiler:2.13'

  // Firebase Core and Crashlytics: Analytics, Crash Reporting
  implementation 'com.google.firebase:firebase-crashlytics:17.2.1'
  implementation 'com.google.firebase:firebase-analytics-ktx:17.5.0'

  // CSV export
  implementation("com.opencsv:opencsv:4.0") {
    exclude group: 'commons-logging', module: 'commons-logging'
  }

  // Compose
  implementation 'androidx.activity:activity-compose:1.4.0'
  implementation 'androidx.compose.material:material:1.1.1'
  implementation 'androidx.compose.ui:ui-tooling:1.1.1'
  implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1'
  implementation "com.google.accompanist:accompanist-appcompat-theme:0.16.0"

  // AndroidX KTX Extensions
  implementation "androidx.core:core-ktx:1.7.0"
  implementation "androidx.activity:activity-ktx:1.4.0"
  implementation "androidx.fragment:fragment-ktx:1.4.1"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

  // Testing
  testImplementation 'junit:junit:4.12'
  androidTestImplementation "androidx.test:runner:$android_test_version"
  androidTestImplementation "androidx.test:rules:$android_test_version"
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
  androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
  androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
  implementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"

  // Test helpers for LiveData
  androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
  // Test helpers for Room
  testImplementation "androidx.room:room-testing:$room_version"
  implementation "androidx.room:room-ktx:$room_version"
}

task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  main = "com.pinterest.ktlint.Main"
  classpath = configurations.ktlint
  args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  main = "com.pinterest.ktlint.Main"
  classpath = configurations.ktlint
  args "-F", "src/**/*.kt"
}
